group 'com.github.albertosh'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "com.github.albertosh.flitetrakr.MainKt"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}


sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'io.kotlintest:kotlintest:1.3.5'
    testCompile 'org.mockito:mockito-core:2.2.9'
    testCompile "com.nhaarman:mockito-kotlin:0.10.1"

    compile project(':core_kotlin')

    compile 'io.reactivex.rxjava2:rxjava:2.0.0'

}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.albertosh.flitetrakr.MainKt'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}